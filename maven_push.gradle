apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.example.applications"
archivesBaseName = "example-application"
version = "1.4.7"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'CreditCardView'
                packaging 'aar'
                // optionally artifactId can be defined here
                description 'A custom Android view for displaying credit card information.'
                url 'https://github.com/naweinberger/CreditCardView'

                scm {
                    connection 'scm:svn:https://github.com/naweinberger/CreditCardView.git'
                    developerConnection 'scm:svn:https://github.com/naweinberger/CreditCardView.git'
                    url 'https://github.com/naweinberger/CreditCardView.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'naweinberger'
                        name 'Natan Weinberger'
                        email 'naweinberger@gmail.com'
                    }
                }
            }
        }
    }
}